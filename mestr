C     PROGRAM ELSYM5 (INPUT,TAPE5=INPUT,OUTPUT,TAPE6=OUTPUT)
C *** CIVIL ENGINEERING LIBRARY VERSION 6/82
C *** IBM 4341, VM/SP CMS OPERATING SYSTEM, FORTRAN H COMPILER
C *** UNIVERSITY OF CALIFORNIA AT BERKELEY
C *** REFERENCES - NONE
C
C *** VERSION  5/80  LEVEL 3A   UNPUBLISHED COMPUTER APPLICATION
C *** ELSYM5 - ELASTIC LAYERED SYSTEM - CONTROL PROGRAM
C *** NUMBER OF ELASTIC LAYERS , MIN OF 1 , MAX OF 5
C *** NUMBER OF LOADS , MIN OF 1 , MAX OF 10
C *** NUMBER OF XY VALUES , MIN OF 1 , MAX OF 10
C *** NUMBER OF Z VALUES , MIN OF 1 , MAX OF 10
C *** BASE MAY BE 'ELASTIC' OR 'RIGID'
C *** WITH RIGID BASE , MAY BE 'FULL FRICTION' OR 'NO FRICTION'
C *** APPROX FORM OF INTEGRATION
C *** GAUSE FOUR POINT WITH NBZ=46
C *** GALE AHLBORN - CIVIL ENGINEERING DEPARTMENT
C                  - TRANSPORTATION ENGINEERING DIVISION
C                  - UNIVERSITY OF CALIFORNIA BERKELEY CA 94720
C
      COMMON E(5),V(5),DI(5),R(100),Z(10),EX(32),
     1        CDAB(5,736),AJ1(184),RJ1(184),RJ0(184),
     2        VSE,SSE,RDP,VDP,RSE,TSE,ST1,ST2,ST3,ST4,
     3        WR,WZ,WRL,RL,PRES,RLP,TST1,TST2,TST3,TST4,
     4        NSYM,NSY,NLSW,NBZ,NGQP,NX,NEX,NR,NRC,NZ,NZC,MSW,
     5        NEL,NEI,NI,NBLL,LAY,NTEST,NTSI,ITS,IND,NAB,NBZC,
     6        KSW1,KSW2,KSW3,KSW4,KSW5,KSW6,KSW7,KSW8,KSW9
C
      COMMON XL(10),YL(10),XP(10),YP(10),LAYZ(10),
     1  ANS(6,100,10),AS(9,3,10),TITLE(14),IXR(100),NLD,NXY
C
      COMMON / COM2 / GP(184)
C
C     ARQUIVO DE DADOS
      OPEN (5,FILE='DADOS.DAD')
C     ARQUIVO DE SAIDA (IMPRESSORA)
      OPEN (6,FILE='OUT.DAT')
C
C
      NBZ=46
      NGQP=4
      TST1=68.0
      TST2=0.0001
      NSY=1
C *** INPUT NUMBER OF SYSTEMS (NSYM)
      READ(5,1000) NSYM
C *** TEST NUMBER OF SYSTEMS
 1000 FORMAT ( I5 )
      IF(NSYM) 9902,9902,3
    3 IF(NSYM-5) 4,4,9901
    4 CALL ELSY1
    2 CALL ELSYI
      GO TO ( 20,20,25),NLSW
   25 CALL ELSY2A
      GO TO 16
   20 CALL ELSY2
   21 K4=1
      K1=1
      DO 22 K2=1,NBZ
      DO 22 K3=1,NGQP
      FM=GP(K1)/WRL
      CALL ELSY3 ( FM )
      CALL ELSY3A ( FM )
      CALL ELSY4 (FM,K4)
      K1=K1+1
      K4=K4+4
   22 CONTINUE
   16 K1=1
      RLDWRL=RL/WRL
      DO 24 K2=1,NBZ
      DO 24 K3=1,NGQP
      X=GP(K1)*RLDWRL
      AJ1(K1)=ELSY12(X,1)
      K1=K1+1
   24 CONTINUE
      GO TO ( 36,51),KSW3
   51 NTEST=2
      GO TO 34
   36 K1=1
      WRDWRL=WR/WRL
      DO 26 K2=1,NBZ
      DO 26 K3=1,NGQP
      X=GP(K1)*WRDWRL
      RJ0(K1)=ELSY12(X,0)
      RJ1(K1)=ELSY12(X,1)
      K1=K1+1
   26 CONTINUE
      IF(WR-RL) 49,49,50
   49 NTEST=RL/WR+0.0001
      GO TO 34
   50 NTEST=WR/RL+0.0001
   34 NTEST=NTEST+1
      IF(NTEST-10) 53,53,52
   52 NTEST=10
   53 NTSI=NTEST+1
      DO 47 K1=1,NZ
      KSW4=1
      NZC=K1
      WZ=Z(NZC)
      IF(WZ) 41,40,41
   40 KSW4=2
   41 GO TO ( 43,43,42),NLSW
   42 LAY=1
      GO TO 46
   43 DO 45 K2=1,NEI
      IF(WZ-(DI(K2)+0.001)) 44,44,45
   44 LAY=K2
      GO TO 46
   45 CONTINUE
      LAY=NEL
   46 LAYZ(NZC)=LAY
      CALL ELSY8
      CALL ELSY9
   47 CONTINUE
      KSW3=1
      NRC=NRC+1
      IF(NRC-NR) 48,48,33
   48 WR=R(NRC)
      IF(WR-RL) 36,36,54
   54 WRL=WR
      GO TO ( 21,21,25),NLSW
   33 CALL ELSY11
      NSY=NSY+1
      IF(NSY-NSYM ) 2,2,35
C *** NSYM ERROR ROUTINE
 9901 WRITE(6,1020)
 1020 FORMAT (1H1,4X,'ELSYM5  5/80 - 3A, ELASTIC LAYERED SYSTEM WITH ONE
     1 TO TEN NORMAL IDENTICAL CIRCULAR UNIFORM LOAD(S)')
      WRITE(6,9001) NSYM
 9001 FORMAT (1H0,4X,'NUMBER OF SYSTEMS REQUESTED IS GREATER THAN 5 (',
     1 I5, ')',/1H0,4X,'RESET TO VALUE OF 5 AND CONTINUING')
      NSYM=5
      GO TO 4
 9902 WRITE(6,1020)
      WRITE(6,9002) NSYM
 9002 FORMAT (1H0,4X,'NUMBER OF SYSTEMS REQUESTED IS LESS THAN 1 (',
     1 I6,')',/1H0,4X,'OPERATION TERMINATED - CHECK INSTRUCTIONS')
   35 CLOSE (5)
      CLOSE (6)
      STOP
      END
      SUBROUTINE ELSYI
C *** FOR USE WITH ELSYM5
C *** ELSYI  - ELASTIC LAYERED SYSTEM - INPUT SECTION
C *** INPUT ELASTIC SYSTEM VALUES,CHECK AND INDICATE
C *** ERRORS,SET SYSTEM LOGIC SWITCHES AND OUTPUT
C *** SYSTEM DESCRIPTION
C
C
      COMMON E(5),V(5),DI(5),R(100),Z(10),EX(32),
     1        CDAB(5,736),AJ1(184),RJ1(184),RJ0(184),
     2        VSE,SSE,RDP,VDP,RSE,TSE,ST1,ST2,ST3,ST4,
     3        WR,WZ,WRL,RL,PRES,RLP,TST1,TST2,TST3,TST4,
     4        NSYM,NSY,NLSW,NBZ,NGQP,NX,NEX,NR,NRC,NZ,NZC,MSW,
     5        NEL,NEI,NI,NBLL,LAY,NTEST,NTSI,ITS,IND,NAB,NBZC,
     6        KSW1,KSW2,KSW3,KSW4,KSW5,KSW6,KSW7,KSW8,KSW9
C
      COMMON XL(10),YL(10),XP(10),YP(10),LAYZ(10),
     1  ANS(6,100,10),AS(9,3,10),TITLE(14),IXR(100),NLD,NXY
C
C *** EXCLUSIVE ROUTINE ARRAYS
      DIMENSION TH(5),XR(100)
C
C *** ALPHA CONDITION OF INTERFACE
      CHARACTER ERMSG(12,8)
      INTEGER AFF,ANF,CI
      DATA AFF,ANF/2HFF,2HNF/
C
C
C *** ERROR MESSAGES ARRAY
C *** ERROR MESSAGES
      DATA ERMSG /
     1 'NUMB','ER O','F EL','ASTI','C LA','YERS',' OUT','SIDE',' LIM',
     + 'IT O','F 1 ','- 5',
     2 'NUMB','ER O','F XY',' VAL','UES ','OUTS','IDE ','LIMI','T OF',
     + ' 1 -',' 10',' ',
     3 'NUMB','ER O','F Z ','VALU','ES O','UTSI','DE L','IMIT',' OF ',
     + '1 - ','10',' ',
     4 'LOAD',' IMP','ROPE','RLY ','DESC','RIBE','D',' ',' ',' ',
     + ' ',' ',
     5 'ELAS','TIC ','LAYE','R DA','TA I','MPRO','PER',' ',' ',' ',
     + ' ',' ',
     6 'Z VA','LUE ','IMPR','OPER',' ',' ',' ',' ',' ',' ',' ',' ',
     7 'NUMB','ER O','F LO','ADS ','OUTS','IDE ','LIMI','T OF',' 1 -',
     + ' 10',' ',' ',
     8 'RIGI','D BA','SE I','NTER','FACE',' IMP','ROPE','RLY ','DEFI',
     + 'NED',' ',' '/
C
      IND1=0
   45 KSW44=1
    1 WRITE(6,1020)
 1020 FORMAT (1H1,4X,'ELSYM5  5/80 - 3A, ELASTIC LAYERED SYSTEM WITH ONE
     1 TO TEN NORMAL IDENTICAL CIRCULAR UNIFORM LOAD(S)')
C *** SEEK SYSTEM TITLE CARD
    2 READ(5,1000) NINE,TITLE
 1000 FORMAT ( I3,1X,14A4 )
C *** TEST ID
      IF(NINE-999) 13,21,13
   13 GO TO (15,2),KSW44
   15 KSW44=2
      WRITE(6,1023) NSY
 1023 FORMAT (1H0,4X,'**** ONE OR MORE DATA CARDS BYPASSED FOR SYSTEM',
     1 I4,' ****')
      GO TO 1
C *** INPUT SYSTEM PARAMETERS
   21 READ(5,1002) NEL,NLD,NXY,NZ
 1002 FORMAT ( 4I5 )
C *** TEST SYSTEM PARAMETERS
C *** TEST NUMBER OF ELASTIC LAYERS (NEL)
C *** SET NUMBER OF LAYERS SWITCH (NLSW)
      IF(NEL-1) 9003,7,6
C *** NLSW=3 FOR 1 ELASTIC LAYER
    7 NLSW=3
      GO TO 8
    6 IF(NEL-3) 3,9,9
C *** NLSW=2 FOR 2 ELASTIC LAYERS
    3 NLSW=2
      GO TO 8
C *** NLSW=1 FOR 3 OR MORE ELASTIC LAYERS
    9 NLSW=1
   11 IF(NEL-5) 8,8,9003
C *** TEST NUMBER OF LOADS (NLD)
    8 IF(NLD) 9009,9009,10
   10 IF(NLD-10) 4,4,9009
C *** TEST NUMBER OF XY VALUES (NXY)
    4 IF(NXY) 9004,9004,5
    5 IF(NXY-10) 12,12,9004
C *** TEST NUMBER OF Z VALUES (NZ)
   12 IF(NZ) 9005,9005,14
   14 IF(NZ-10) 16,16,9005
C *** INPUT LAYER DATA AND TEST VALUES
   16 DO 54 K1=1,NEL
      READ(5,1004) LN,TH(K1),V(K1),E(K1),CI
 1004 FORMAT ( I5,2F5.0,F10.0,4X,A2)
      IF(LN-K1) 9007,44,9007
   44 IF(E(K1)) 9007,9007,46
   46 IF(V(K1)) 9007,9007,47
   47 IF(V(K1)-.99) 48,48,9007
   48 IF(K1-NEL) 52,50,9007
   52 IF(TH(K1)) 9007,9007,54
C *** TEST BASE CONDITION
   50 IF(TH(K1)) 9007,18,22
C *** SET BASE CONDITION TO ELASTIC
   18 NI=NEL-1
      NX=2
      KSW1=1
      GO TO 24
C *** SET BASE CONDITION TO RIGID
   22 NI=NEL
      NX=1
      KSW1=2
C *** CHECK BASE INTERFACE CONDITION
      IF(CI .NE. AFF) GO TO 53
      KSW2=1
      GO TO 24
   53 IF(CI .NE. ANF) GO TO 9010
      KSW2=2
   24 NEI=NEL-1
   54 CONTINUE
C *** INPUT NORMAL LOAD VALUES
      READ(5,1003) FOR,PRES,RL
C *** TEST NORMAL LOAD VALUES
 1003 FORMAT ( 3F10.0 )
C *** TEST FOR TWO OUT OF THREE AND NEGATIVE VALUES
C *** TEST FORCE VALUE (FOR)
      IF(FOR) 9006,26,28
C *** TEST PRESSURE VALUE (PRES)
   26 IF(PRES) 9006,9006,30
   28 IF(PRES) 9006,32,34
C *** TEST RADIUS OF LOAD VALUE (RL)
   30 IF(RL) 9006,9006,36
   32 IF(RL) 9006,9006,38
   34 IF(RL) 9006,40,9006
C *** COMPUTE MISSING VALUE
   36 FOR=PRES*RL**2/0.31830987
      GO TO 42
   38 PRES=0.31830987*FOR/(RL**2)
      GO TO 42
   40 RL=SQRT(0.31830987*FOR/PRES)
C *** INPUT NORMAL LOAD COORDINATES
   42 READ(5,1025) (XL(K1),YL(K1),K1=1,NLD )
 1025 FORMAT ( 2F10.0 )
C *** INPUT COORDINATES FOR XY POINTS
      READ(5,1025) (XP(K1),YP(K1),K1=1,NXY)
C *** INPUT Z VALUES AND TEST
      READ(5,1005) (Z(K1),K1=1,NZ)
 1005 FORMAT (10F5.0 )
C *** CHECK FOR NEGATIVE Z VALUE
      DO 58 K1=1,NZ
      IF(Z(K1)) 9008,58,58
   58 CONTINUE
C *** SORT Z VALUES IN ORDER OF INCREASING VALUE
   74 IF(NZ-1) 67,67,61
   61 K1=NZ-1
      DO 65 K2=1,K1
      K3=K2+1
      DO 65 K4=K3,NZ
      IF(Z(K2)-Z(K4)) 65,9008,63
   63 TEMP=Z(K2)
      Z(K2)=Z(K4)
      Z(K4)=TEMP
   65 CONTINUE
C *** DEVELOP DEPTH TO INTERFACE ARRAY
   67 GO TO ( 77,77,75 ),NLSW
   75 DI(1)=TH(1)
      EX(1)=0.0
      EX(2)=2.0*TH(1)
      GO TO 85
   77 DI(1)=TH(1)
      IF(NI-1) 80,80,76
   76 DO 78 K1=2,NI
      DI(K1)=DI(K1-1)+TH(K1)
   78 CONTINUE
C *** DEVELOP EXPONENT TEST ARRAY
   80 NEX=2**NI
      K1=1
      K2=1
      EX(1)=0.0
      DO 84 K3=1,NEL
      K4=NEL+1-K3
      TEMP=2.0*TH(K4)
      DO 82 K5=1,K2
      K1=K1+1
      EX(K1)=TEMP+EX(K5)
   82 CONTINUE
      K2=K2*2
   84 CONTINUE
   85 GO TO ( 87,83),KSW1
C *** ELIMINATE Z VALUES .GT. DEPTH TO RIGID BASE
C     AND ADJUST NUMBER OF Z VALUES
   83 DINI=DI(NI)
      NZT=NZ
      DO 89 K1=1,NZ
      K2=NZ-K1+1
      IF(DINI+0.0001-Z(K2)) 95,89,89
   95 NZT=NZT-1
   89 CONTINUE
      NZ=NZT
C *** TEST V(NEL) NEAR 0.75
      IF(V(NEL)-0.748 ) 87,87,81
   81 IF(V(NEL)-0.752) 9007,87,87
C *** DEVELOP R ARRAY BY LOAD WITHIN XY POINT
   87 NXR=NXY*NLD
      K1=1
      DO 17 K2=1,NXY
      XPI=XP(K2)
      YPI=YP(K2)
      DO 17 K3=1,NLD
      XR(K1)=SQRT((YPI-YL(K3))**2+(XPI-XL(K3))**2)
      K1=K1+1
   17 CONTINUE
C *** DEVELOP R ARRAY IN INCREASING ORDER WITHOUT
C     DUPLICATE OR NEAR DUPLICATE VALUES
      NR=0
      K1=1
      R(1)=XR(1)
   19 NR=NR+1
   20 K1=K1+1
      IF(K1 .GT. NXR) GO TO 25
      RT=XR(K1)
C *** TEST FOR DUPLICATE OR NEAR DUPLICATE VALUES
      DO 23 K2=1,NR
      IF(ABS(RT-R(K2)) .LE. 0.02) GO TO 20
   23 CONTINUE
C *** INSERT VALUE IN PROPER ORDER
      DO 35 K2=1,NR
      K3=NR+1-K2
      IF(RT .LT. R(K3)) GO TO 33
      R(K3+1)=RT
      GO TO 19
   33 R(K3+1)=R(K3)
   35 CONTINUE
      R(K3)=RT
      GO TO 19
C
C *** DEVELOP IXR ARRAY AS A POINTER
   25 DO 31 K1=1,NXR
      XRI=XR(K1)
      DO 29 K2=1,NR
      IF(ABS(XRI-R(K2)) .GT. 0.02) GO TO 29
      IXR(K1)=K2
      GO TO 31
   29 CONTINUE
      WRITE(6,1010)
 1010 FORMAT ( 1H ,12HERROR IN IRX )
      CLOSE (5)
      CLOSE (6)
      STOP
   31 CONTINUE
      IF(ABS(RL-R(1))-0.0001) 86,86,72
   72 IF(RL-R(1)) 88,86,86
   86 WRL=RL
      GO TO 90
   88 WRL=R(1)
   90 WR=R(1)
      IF(WR-0.0001) 91,91,92
   91 WR=0.0
      KSW3=2
      GO TO 93
   92 KSW3=1
   93 NRC=1
C *** WITH SORTED Z VALUES - ONLY TEST ONE VALUE
C *** DETERMINE BACK SUBSTITUTION LIMIT
      ZT=Z(NZ)
      DO 71 K1=1,NEI
      NBLL=K1
      IF(ZT-DI(K1)) 73,73,71
   71 CONTINUE
      NBLL=NEL
C *** SET RELATIVE (PSI), OR ABSOLUTE OUTPUT
   73 IF(IND1) 96,97,96
   96 RLP=RL
      GO TO 94
   97 RLP=RL*PRES
C *** OUTPUT ELASTIC SYSTEM DESCRIPTION
   94 WRITE(6,1012) NSY,TITLE
 1012 FORMAT (1H0,4X,14HELASTIC SYSTEM,I3,3H - ,14A4)
      WRITE(6,1022)
 1022 FORMAT (
     1 1H0,14X,18HELASTIC   POISSONS/1H ,4X,5HLAYER,5X,7HMODULUS,3X,
     2 5HRATIO,6X,9HTHICKNESS )
      GO TO ( 51,51,57),NLSW
   51 DO 60 K1=1,NEI
      WRITE(6,1007) K1,E(K1),V(K1),TH(K1)
 1007 FORMAT (1H ,4X,I3,F14.0,4X,F5.3,5X,F8.3,3H IN )
   60 CONTINUE
   57 GO TO ( 62,64),KSW1
   62 WRITE(6,1008) NEL,E(NEL),V(NEL)
 1008 FORMAT (1H ,4X,I3,F14.0,4X,F5.3,5X,13HSEMI-INFINITE )
      GO TO 66
   64 GO TO (56,59),KSW2
   56 WRITE(6,1009) NEL,E(NEL),V(NEL),TH(NEL)
 1009 FORMAT (1H ,4X,I3,F14.0,4X,F5.3,5X,F8.3,' IN  RIGID BASE',
     1 ' , FULL FRICTION')
      GO TO 66
   59 WRITE(6,1024) NEL,E(NEL),V(NEL),TH(NEL)
 1024 FORMAT (1H ,4X,I3,F14.0,4X,F5.3,5X,F8.3,' IN  RIGID BASE',
     1 ' , NO FRICTION')
   66 WRITE(6,1001) NLD
 1001 FORMAT (1H0,4X,I3,1X,'LOAD(S), EACH LOAD AS FOLLOWS')
      WRITE(6,1006) FOR,PRES,RL
 1006 FORMAT ( 1H0,4X,16HTOTAL LOAD..... ,F10.2,4H LBS/1H ,4X,16HLOAD ST
     1RESS.... ,F10.2,4H PSI/1H ,4X,16HLOAD RADIUS....  ,F10.2,3H IN )
      WRITE(6,1021) (K1,XL(K1),YL(K1),K1=1,NLD )
 1021 FORMAT ( 1H0,11X,10HLOCATED AT / 1H ,4X,4HLOAD,4X,1HX,8X,1HY /
     1 (1H ,4X,I3,1X,F8.3,1X,F8.3))
      WRITE(6,1014)
 1014 FORMAT (1H0,4X,'RESULTS REQUESTED FOR SYSTEM LOCATION(S)')
      WRITE(6,1015)
 1015 FORMAT (1H0,4X,'DEPTH(S)')
      WRITE(6,1016) (Z(K1),K1=1,NZ)
 1016 FORMAT (1H ,4X,'Z='10F7.2)
      WRITE(6,1017)
 1017 FORMAT (1H ,4X,'X-Y POINT(S)')
      WRITE(6,1018) (XP(K1),K1=1,NXY)
 1018 FORMAT (1H ,4X,'X=',10F7.2)
      WRITE(6,1019) (YP(K1),K1=1,NXY)
 1019 FORMAT (1H ,4X,'Y=',10F7.2)
      RETURN
C *** INPUT DATA ERROR TRAP
C *** SET ERROR INDICATOR
 9003 NER=1
      GO TO 8001
 9004 NER=2
      GO TO 8001
 9005 NER=3
      GO TO 8001
 9006 NER=4
      GO TO 8001
 9007 NER=5
      GO TO 8001
 9008 NER=6
      GO TO 8001
 9009 NER=7
      GO TO 8001
 9010 NER=8
C *** COMMON ERROR OUTPUT 1
 8001 WRITE(6,8002) NSY,TITLE
 8002 FORMAT ( 1H0,4X,14HELASTIC SYSTEM,I5,3H - ,14A4 /
     1 1H0,4X,16HINPUT DATA ERROR )
C *** ERROR CONDITION OUTPUT
      WRITE(6,9803) (ERMSG(K1,NER),K1=1,12)
 9803 FORMAT (1H0,4X,12A4)
      NSY=NSY+1
      IF(NSY-NSYM) 45,45,69
   69 CLOSE (5)
      CLOSE (6)
      STOP
      END
      SUBROUTINE ELSY1
C *** FOR USE WITH ELSYM5
C *** ELSY1 DEVELOP EXPANDED M VALUES
C
      COMMON E(5),V(5),DI(5),R(100),Z(10),EX(32),
     1        CDAB(5,736),AJ1(184),RJ1(184),RJ0(184),
     2        VSE,SSE,RDP,VDP,RSE,TSE,ST1,ST2,ST3,ST4,
     3        WR,WZ,WRL,RL,PRES,RLP,TST1,TST2,TST3,TST4,
     4        NSYM,NSY,NLSW,NBZ,NGQP,NX,NEX,NR,NRC,NZ,NZC,MSW,
     5        NEL,NEI,NI,NBLL,LAY,NTEST,NTSI,ITS,IND,NAB,NBZC,
     6        KSW1,KSW2,KSW3,KSW4,KSW5,KSW6,KSW7,KSW8,KSW9
C
      COMMON XL(10),YL(10),XP(10),YP(10),LAYZ(10),
     1  ANS(6,100,10),AS(9,3,10),TITLE(14),IXR(100),NLD,NXY
C
      COMMON / COM2 / GP(184)
C
C *** EXCLUSIVE ROUTINE ARRAYS
      REAL BZ(46),GU(4)
C
      DATA BZ /  1.0,
     1 2.40482556 , 3.83170597 , 5.52007811 , 7.01558667 , 8.65372791 ,
     2 10.1734681 , 11.7915344 , 13.3236919 , 14.9309177 , 16.4706301 ,
     3 18.0710640 , 19.6158585 , 21.2116366 , 22.7600844 , 24.3524715 ,
     4 25.9036721 , 27.4934791 , 29.0468285 , 30.6346065 , 32.1896799 ,
     5 33.7758202 , 35.3323076 , 36.9170984 , 38.4747662 , 40.0584258 ,
     6 41.6170942 , 43.1997917 , 44.7593190 , 46.3411884 , 47.9014609 ,
     7 49.4826099 , 51.0435352 , 52.6240518 , 54.1855536 , 55.7655108 ,
     8 57.3275254 , 58.9069839 , 60.4694578 , 62.0484692 , 63.6113567 ,
     9 65.1899648 , 66.7532267 , 68.3314693 , 69.8950718 , 71.4729816 /
C
      DATA GU /
     1-.430568156 ,-.169990522 , .169990522 , .430568156 /
C
      K3=0
      FMB=0.0
      DO 20 K1=1,NBZ
      FM=BZ(K1)
      SBA=(FM+FMB)/2.0
      DBA=FM-FMB
      DO 22 K2=1,NGQP
      K3=K3+1
      GP(K3)=SBA+DBA*GU(K2)
   22 CONTINUE
      FMB=FM
   20 CONTINUE
      RETURN
      END
      SUBROUTINE ELSY2A
C *** FOR USE WITH ELSYM5
C *** ELSY2A
C *** SINGLE LAYER SYSTEM
C
      COMMON E(5),V(5),DI(5),R(100),Z(10),EX(32),
     1        CDAB(5,736),AJ1(184),RJ1(184),RJ0(184),
     2        VSE,SSE,RDP,VDP,RSE,TSE,ST1,ST2,ST3,ST4,
     3        WR,WZ,WRL,RL,PRES,RLP,TST1,TST2,TST3,TST4,
     4        NSYM,NSY,NLSW,NBZ,NGQP,NX,NEX,NR,NRC,NZ,NZC,MSW,
     5        NEL,NEI,NI,NBLL,LAY,NTEST,NTSI,ITS,IND,NAB,NBZC,
     6        KSW1,KSW2,KSW3,KSW4,KSW5,KSW6,KSW7,KSW8,KSW9
      COMMON XL(10),YL(10),XP(10),YP(10),LAYZ(10),
     1  ANS(6,100,10),AS(9,3,10),TITLE(14),IXR(100),NLD,NXY
C
      COMMON / COM2 / GP(184)
C
C *** EXCLUSIVE ROUTINE ARRAYS
      DIMENSION SA(2,2)
C
      ST1=2.0*V(1)
      ST2=ST1-1.0
      GO TO ( 10,5),KSW1
C *** SINGLE ELASTIC LAYER WITH RIGID BASE
    5 PC1=1.0/(3.0-4.0*V(1))
      PC2=1.0-4.0*V(1)
      PC3=2.0*PC1
      PC4=2.0*DI(1)
      PC5=PC4*DI(1)
      PC6=4.0*ST2
C *** END RIGID BASE ROUTINE
   10 K6=-3
      K5=0
      DO 40 K2=1,NBZ
      DO 40 K1=1,NGQP
      K6=K6+4
      K5=K5+1
      FM=GP(K5)/WRL
      RFM=1.0/FM
      RFM2=RFM*RFM
      GO TO ( 30,20),KSW1
C *** SINGLE ELASTIC LAYER WITH RIGID BASE
   20 VAL=FM*EX(2)
C *** SINGLE ELASTIC LAYER WITH RIGID BASE REVERTS TO
C *** SINGLE ELASTIC LAYER WITH NO BASE WHEN VAL EXCEEDS TEST
      IF(VAL-TST1) 21,21,30
   21 VAL=EXP(-VAL)
      GO TO (22,24),KSW2
C *** RIGID BASE WITH FULL FRICTION
   22 CM1=(PC2-FM*PC4)*PC1*VAL
      CM2=(PC6-FM*FM*PC5)*PC1*RFM*VAL
      CM3=PC3*FM*VAL
      CM4=(PC2+FM*PC4)*PC1*VAL
      SA(1,1)=FM+FM*CM1+ST1*CM3
      SA(1,2)=-ST1+FM*CM2+ST1*CM4
      SA(2,1)=-FM+FM*CM1+ST2*CM3
      SA(2,2)=ST2+FM*CM2+ST2*CM4
      GO TO 23
C *** RIGID BASE WITH NO FRICTION
   24 SA(1,1)=FM-FM*VAL
      SA(1,2)=-ST1+(ST1-2.0*FM*DI(1))*VAL
      SA(2,1)=-FM-FM*VAL
      SA(2,2)=ST2+(ST2-2.0*FM*DI(1))*VAL
   23 CDAB(1,K6)=-SA(1,2)*RFM2/(SA(2,1)*(-SA(1,2))+SA(2,2)*SA(1,1))
      CDAB(1,K6+1)=SA(1,1)*CDAB(1,K6)/(-SA(1,2))
      GO TO (25,26),KSW2
C *** RIGID BASE WITH FULL FRICTION
   25 CDAB(1,K6+2)=CM1*CDAB(1,K6)+CM2*CDAB(1,K6+1)
      CDAB(1,K6+3)=CM3*CDAB(1,K6)+CM4*CDAB(1,K6+1)
      GO TO 40
C *** RIGID BASE WITH NO FRICTION
   26 CDAB(1,K6+2)=-(CDAB(1,K6)+2.0*DI(1)*CDAB(1,K6+1))*VAL
      CDAB(1,K6+3)=CDAB(1,K6+1)*VAL
      GO TO 40
C *** SINGLE ELASTIC LAYER - NO BASE
   30 CDAB(1,K6)=-ST1*RFM2*RFM
      CDAB(1,K6+1)=-RFM2
      CDAB(1,K6+2)=0.0
      CDAB(1,K6+3)=0.0
   40 CONTINUE
      RETURN
      END
      SUBROUTINE ELSY2
C *** FOR USE WITH ELSYM5
C *** ELSY2
C
C *** ELSYM - PRE COEE
C
C
C
C
C *** GO OVER THIS PROGRAM TO IMPROVE
C
C
C
      COMMON E(5),V(5),DI(5),R(100),Z(10),EX(32),
     1        CDAB(5,736),AJ1(184),RJ1(184),RJ0(184),
     2        VSE,SSE,RDP,VDP,RSE,TSE,ST1,ST2,ST3,ST4,
     3        WR,WZ,WRL,RL,PRES,RLP,TST1,TST2,TST3,TST4,
     4        NSYM,NSY,NLSW,NBZ,NGQP,NX,NEX,NR,NRC,NZ,NZC,MSW,
     5        NEL,NEI,NI,NBLL,LAY,NTEST,NTSI,ITS,IND,NAB,NBZC,
     6        KSW1,KSW2,KSW3,KSW4,KSW5,KSW6,KSW7,KSW8,KSW9
C
      COMMON XL(10),YL(10),XP(10),YP(10),LAYZ(10),
     1  ANS(6,100,10),AS(9,3,10),TITLE(14),IXR(100),NLD,NXY
C
      COMMON / COM1 /
     1 EI(16,2,2),S(2,2,2),SV(4,9),ISC(4),SA(2,2),RB(2,2),BA(2,2,2),
     2 TB1,TB2,TB3,TB4,TB5,
     3 NE,NEXD2,NEIM1
C
C
C *** EXCLUSIVE ROUTINE ARRAYS
      DIMENSION RDET(4)
C
      DO 32 K1=1,NEI
      RDET(K1)= 1.0/( 4.0*( 1.0-V(K1)))
   32 CONTINUE
      NEXD2=NEX/2
      NEIM1=NEI-1
      NE=NEIM1*4+1
C *** DEVELOP ISC ARRAY
      ISC(NEI)=1
      GO TO ( 42,44),NLSW
   42 DO 30 K1=1,NEIM1
      K2=NEI-K1
      ISC(K2)=ISC(K2+1)*2
   30 CONTINUE
   44 K1=1
      DO 10 K2=1,NE,4
      T1=(1.0+V(K1+1))*E(K1)/(E(K1+1)*(1.0+V(K1)))
      T2=1.0-T1
C *** ELEMENTS TOTALLY INDEPENDENT OF M
      EI(K2,1,1)=(-4.0*V(K1)+T1+3.0  )*RDET(K1)
      EI(K2+3,1,1)=EI(K2,1,1)
      EI(K2,2,2)=(-T1*(4.0*V(K1+1)-3.0)+1.0 )*RDET(K1)
      EI(K2+3,2,2)=EI(K2,2,2)
      EI(K2,2,1)=0.0
      EI(K2+3,2,1)=0.0
C *** PORTIONS OF ELEMENTS INDEPENDENT OF M
      SV(K1,1)=(-2.0*DI(K1)*(2.0*V(K1)-1.0-T1*(2.0*V(K1+1)-1.0)))*
     1  RDET(K1)
      SV(K1,2)=(-T2*(4.0*V(K1)-1.0)  )*RDET(K1)
      SV(K1,3)=(-4.0*DI(K1)*(T2*(V(K1)-V(K1+1))))*RDET(K1)
      SV(K1,4)=(-T2*(4.0*V(K1+1)-1.0) )*RDET(K1)
C *** PORTION OF ELEMENTS DEPENDENT ON 1.0/M
      SV(K1,5)=(1.0+2.0*V(K1+1)*(4.0*V(K1)-3.0)
     1  -T1*(1.0+2.0*V(K1)*(4.0*V(K1+1)-3.0))  )*RDET(K1)
      SV(K1,6)=(1.0-2.0*V(K1+1)*(4.0*V(K1)-1.0)
     1  -T1*(1.0-2.0*V(K1)*(4.0*V(K1+1)-1.0)) )*RDET(K1)
C *** PORTIONS OF ELEMENTS DEPENDENT ON M
      T1=(-2.0*T2  )*RDET(K1)
      SV(K1,7)=-DI(K1)*T1
      SV(K1,8)=-DI(K1)**2*T1
      SV(K1,9)=T1
      K1=K1+1
   10 CONTINUE
C *** SET UP SURFACE ARRAY ELEMENTS INDEPENDENT OF M
      S(1,1,2)=-2.0*V(1)
      S(1,2,2)=2.0*V(1)-1.0
      S(2,1,2)=2.0*V(1)
      S(2,2,2)=2.0*V(1)-1.0
      GO TO ( 22,20 ),KSW1
C *** RIGID BASE WITH ROUGH INTERFACE
   20 T1=3.0-4.0*V(NEL)
C *** PORTIONS OF RIGID BASE ELEMENTS INDEPENDENT OF M
      TB1=(1.0-4.0*V(NEL))/T1
C *** PORTIONS OF RIGID BASE ELEMENTS DEPENDENT ON M
      TB3=2.0/T1
      TB4=TB3*DI(NI)
      TB5=TB4*DI(NI)
C *** PORTIONS OF RIGID BASE ELEMENTS DEPENDENT ON 1.0/M
      TB2=4.0*(2.0*V(NEL)-1.0)/T1
   22 RETURN
      END
      SUBROUTINE ELSY3 ( FM )
C *** FOR USE WITH ELSYM5
C *** ELSY3
C
C *** DEVELOP EI(K,I,J), S(K,I,J), AND RB(I,J) ARRAYS FOR M VALUE
C
      COMMON E(5),V(5),DI(5),R(100),Z(10),EX(32),
     1        CDAB(5,736),AJ1(184),RJ1(184),RJ0(184),
     2        VSE,SSE,RDP,VDP,RSE,TSE,ST1,ST2,ST3,ST4,
     3        WR,WZ,WRL,RL,PRES,RLP,TST1,TST2,TST3,TST4,
     4        NSYM,NSY,NLSW,NBZ,NGQP,NX,NEX,NR,NRC,NZ,NZC,MSW,
     5        NEL,NEI,NI,NBLL,LAY,NTEST,NTSI,ITS,IND,NAB,NBZC,
     6        KSW1,KSW2,KSW3,KSW4,KSW5,KSW6,KSW7,KSW8,KSW9
C
      COMMON XL(10),YL(10),XP(10),YP(10),LAYZ(10),
     1  ANS(6,100,10),AS(9,3,10),TITLE(14),IXR(100),NLD,NXY
C
      COMMON / COM1 /
     1 EI(16,2,2),S(2,2,2),SV(4,9),ISC(4),SA(2,2),RB(2,2),BA(2,2,2),
     2 TB1,TB2,TB3,TB4,TB5,
     3 NE,NEXD2,NEIM1
C
C
      RFM= 1.0/FM
      K1=1
C *** SET UP ELEMENTS DEPENDENT ON M AND 1.0/M
      DO 4 K2=1,NE,4
      EI(K2  ,1,2)= SV(K1,1)              +RFM*SV(K1,5)
      EI(K2+1,1,1)= SV(K1,2) +FM*SV(K1,7)
      EI(K2+1,1,2)= SV(K1,3) +FM*SV(K1,8) +RFM*SV(K1,6)
      EI(K2+1,2,1)=           FM*SV(K1,9)
      EI(K2+1,2,2)= SV(K1,4) -FM*SV(K1,7)
      EI(K2+2,1,1)= SV(K1,2) -FM*SV(K1,7)
      EI(K2+2,1,2)= SV(K1,3) -FM*SV(K1,8) -RFM*SV(K1,6)
      EI(K2+2,2,1)=          -FM*SV(K1,9)
      EI(K2+2,2,2)= SV(K1,4) +FM*SV(K1,7)
      EI(K2+3,1,2)= SV(K1,1)              -RFM*SV(K1,5)
      K1=K1+1
    4 CONTINUE
C *** SET UP SURFACE ARRAY ELEMENTS THAT ARE DEPENDENT ON M
      S(1,1,1)=FM
      S(1,2,1)=-FM
      S(2,1,1)=FM
      S(2,2,1)=FM
      GO TO ( 40,30),KSW1
   30 GO TO (50,51),KSW2
C *** RIGID BASE WITH FULL FRICTION
   50 RB(1,1)=TB1-FM*TB4
      RB(1,2)=RFM*TB2-FM*TB5
      RB(2,1)=FM*TB3
      RB(2,2)=TB1+FM*TB4
      GO TO 52
C *** RIGID BASE WITH NO FRICTION
   51 RB(1,1)=-1.0
      RB(1,2)=-2.0*DI(NI)
      RB(2,1)=0.0
      RB(2,2)=1.0
   52 K7=1
      K1=NE+1
      K3=K1+2
      DO 38 K2=K1,K3,2
      DO 32 K4=1,2
      DO 32 K5=1,2
      SUM=0.0
      DO 31 K6=1,2
      SUM=SUM+EI(K2,K4,K6)*RB(K6,K5)
   31 CONTINUE
      BA(K7,K4,K5)=SUM
   32 CONTINUE
      K7=K7+1
   38 CONTINUE
   40 RETURN
      END
      SUBROUTINE ELSY3A ( FM )
C *** FOR USE WITH ELSYM5
C *** ELSY3A
C
C *** MULTIPLY S(K,I,J) AND EI(K,I,J) FOR ALL M VALUES
C
      COMMON E(5),V(5),DI(5),R(100),Z(10),EX(32),
     1        CDAB(5,736),AJ1(184),RJ1(184),RJ0(184),
     2        VSE,SSE,RDP,VDP,RSE,TSE,ST1,ST2,ST3,ST4,
     3        WR,WZ,WRL,RL,PRES,RLP,TST1,TST2,TST3,TST4,
     4        NSYM,NSY,NLSW,NBZ,NGQP,NX,NEX,NR,NRC,NZ,NZC,MSW,
     5        NEL,NEI,NI,NBLL,LAY,NTEST,NTSI,ITS,IND,NAB,NBZC,
     6        KSW1,KSW2,KSW3,KSW4,KSW5,KSW6,KSW7,KSW8,KSW9
C
      COMMON XL(10),YL(10),XP(10),YP(10),LAYZ(10),
     1  ANS(6,100,10),AS(9,3,10),TITLE(14),IXR(100),NLD,NXY
C
      COMMON / COM1 /
     1 EI(16,2,2),S(2,2,2),SV(4,9),ISC(4),SA(2,2),RB(2,2),BA(2,2,2),
     2 TB1,TB2,TB3,TB4,TB5,
     3 NE,NEXD2,NEIM1
C
C *** EXCLUSIVE ROUTINE ARRAYS
      DIMENSION TP(5,2,2),ITA(4),ITAB(4),SEI(2,2,2)
C
      RFM= 1.0/FM
      GO TO ( 34,32),KSW1
C *** START RIGID BASE ROUTINE
   32 K6=0
      K1=NE+1
      K3=K1+2
      DO 38 K2=K1,K3,2
      K6=K6+1
      DO 38 K4=1,2
      DO 38 K5=1,2
      SEI(K6,K5,K4)=EI(K2,K5,K4)
      EI(K2,K5,K4)=BA(K6,K5,K4)
   38 CONTINUE
C *** END RIGID BASE ROUTINE
   34 DO 28 K1=1,2
      DO 28 K2=1,2
      SA(K1,K2)=0.0
   28 CONTINUE
      ITAB(1)=3
      K5=1
      DO 100 K1=1,2
      DO 5 K2=1,2
      DO 5 K3=1,2
      TP(1,K2,K3)=S(K1,K2,K3)
    5 CONTINUE
      DO 90 K4=1,NEXD2
C *** CHECK EXPONENT VALUE
      VAL=FM*EX(K5)
      IF(VAL-TST1) 6,6,89
    6 VAL=EXP(-VAL)
C *** DEVELOP INDEX TO ADD ARRAY
      DO 10 K2=1,NEI
      IT=K5-1
    7 ITT=IT/ISC(K2)
      IF(ITT-3) 9,9,8
    8 IT=IT-4*ISC(K2)
      GO TO 7
    9 ITA(K2)=ITT
   10 CONTINUE
C *** CHECK AGAINST BACK INDEX TO ADD ARRAY
      DO 15 K2=1,NEI
      K3=K2
      IF(ITA(K2)-ITAB(K2)) 16,15,16
   15 CONTINUE
C *** K3 IS NOW THE INDEX FOR THE TP AND THE ITA ARRAYS
C *** SET ITA BACK ARRAY
   16 DO 26 K2=1,NEI
      ITAB(K2)=ITA(K2)
   26 CONTINUE
      K2=(K3-1)*4+1
      DO 20 K10=K2,NE,4
      K6=K10+ITA(K3)
      DO 22 K7=1,2
      DO 22 K8=1,2
      SUM=0.0
      DO 21 K9=1,2
      SUM=SUM+TP(K3,K7,K9)*EI(K6,K9,K8)
   21 CONTINUE
      TP(K3+1,K7,K8)=SUM
   22 CONTINUE
      K3=K3+1
   20 CONTINUE
      DO 24 K7=1,2
      DO 24 K8=1,2
      SA(K7,K8)=SA(K7,K8)+TP(K3,K7,K8)*VAL
   24 CONTINUE
   89 K5=K5+NX
   90 CONTINUE
  100 CONTINUE
      GO TO ( 85,84),KSW1
C *** START RIGID BASE ROUTINE
C *** REPLACE BOTTOM EI ELEMENTS
   84 K6=0
      K1=NE+1
      K3=K1+2
      DO 83 K2=K1,K3,2
      K6=K6+1
      DO 83 K4=1,2
      DO 83 K5=1,2
      EI(K2,K5,K4)=SEI(K6,K5,K4)
   83 CONTINUE
C *** END RIGID BASE ROUTINE
   85 RETURN
      END
      SUBROUTINE ELSY4 ( FM,M )
C *** FOR USE WITH ELSYM5
C *** ELSY4
C
C *** ELSYM - BACK SOLVE FOR A(I,J),B(I,J),C(I,J) AND D(I,J)
C
      COMMON E(5),V(5),DI(5),R(100),Z(10),EX(32),
     1        CDAB(5,736),AJ1(184),RJ1(184),RJ0(184),
     2        VSE,SSE,RDP,VDP,RSE,TSE,ST1,ST2,ST3,ST4,
     3        WR,WZ,WRL,RL,PRES,RLP,TST1,TST2,TST3,TST4,
     4        NSYM,NSY,NLSW,NBZ,NGQP,NX,NEX,NR,NRC,NZ,NZC,MSW,
     5        NEL,NEI,NI,NBLL,LAY,NTEST,NTSI,ITS,IND,NAB,NBZC,
     6        KSW1,KSW2,KSW3,KSW4,KSW5,KSW6,KSW7,KSW8,KSW9
C
      COMMON XL(10),YL(10),XP(10),YP(10),LAYZ(10),
     1  ANS(6,100,10),AS(9,3,10),TITLE(14),IXR(100),NLD,NXY
C
      COMMON / COM1 /
     1 EI(16,2,2),S(2,2,2),SV(4,9),ISC(4),SA(2,2),RB(2,2),BA(2,2,2),
     2 TB1,TB2,TB3,TB4,TB5,
     3 NE,NEXD2,NEIM1
C
C *** EXCLUSIVE ROUTINE ARRAYS
      DIMENSION VA(4)
C
      VA(1)=1.0
      VA(4)=1.0
      RFM= 1.0/FM
      CDAB(NEL,M  )=-SA(1,2)*RFM*RFM/(-SA(2,1)*SA(1,2)+
     1 SA(2,2)*SA(1,1))
      CDAB(NEL,M+1)=SA(1,1)*CDAB(NEL,M  )/(-SA(1,2))
      GO TO ( 20,22),KSW1
C *** START RIGID BASE ROUTINE
   22 VAL=FM*DI(NI)*2.0
      IF(VAL-TST1) 23,23,20
   23 VAL=EXP(-VAL)
      CDAB(NEL,M+2)=(RB(1,1)*CDAB(NEL,M  )+RB(1,2)*CDAB(NEL,M+1))*VAL
      CDAB(NEL,M+3)=(RB(2,1)*CDAB(NEL,M  )+RB(2,2)*CDAB(NEL,M+1))*VAL
      GO TO 24
C *** END RIGID BASE ROUTINE
   20 CDAB(NEL,M+2)=0.0
      CDAB(NEL,M+3)=0.0
   24 DO 50 K10=1,NEI
      K11=NEI-K10+1
      MP3=M+3
      DO 45 K1=M,MP3
      CDAB(K11,K1)=0.0
   45 CONTINUE
      K12=K11+1
      K13=(K11-1)*4+1
      VAL=2.0*FM*DI(K11)
      IF(VAL-TST1) 47,47,46
   46 K6=1
      K4=1
      GO TO 48
   47 VA(2)=EXP(-VAL)
      K4=2
      VA(3)=1.0/VA(2)
      IF(CDAB(K12,M+2)) 43,41,43
   41 K6=1
      GO TO 48
   43 K6=2
   48 K8=M-2
      K7=0
      DO 44 K5=1,K6
      K2=M-1
      K8=K8+2
      K9=K8+1
      DO 42 K3=1,K4
      K7=K7+1
      DO 40 K1=1,2
      K2=K2+1
      CDAB(K11,K2)=CDAB(K11,K2)+VA(K7)*(EI(K13,K1,1)*
     1  CDAB(K12,K8)+EI(K13,K1,2)*CDAB(K12,K9 ))
   40 CONTINUE
      K13=K13+2
   42 CONTINUE
      K13=K13-3
   44 CONTINUE
   50 CONTINUE
      RETURN
      END
      SUBROUTINE ELSY8
C *** FOR USE WITH ELSYM5
C *** ELSY8
C
      COMMON E(5),V(5),DI(5),R(100),Z(10),EX(32),
     1        CDAB(5,736),AJ1(184),RJ1(184),RJ0(184),
     2        VSE,SSE,RDP,VDP,RSE,TSE,ST1,ST2,ST3,ST4,
     3        WR,WZ,WRL,RL,PRES,RLP,TST1,TST2,TST3,TST4,
     4        NSYM,NSY,NLSW,NBZ,NGQP,NX,NEX,NR,NRC,NZ,NZC,MSW,
     5        NEL,NEI,NI,NBLL,LAY,NTEST,NTSI,ITS,IND,NAB,NBZC,
     6        KSW1,KSW2,KSW3,KSW4,KSW5,KSW6,KSW7,KSW8,KSW9
C
      COMMON XL(10),YL(10),XP(10),YP(10),LAYZ(10),
     1  ANS(6,100,10),AS(9,3,10),TITLE(14),IXR(100),NLD,NXY
C
      COMMON / COM2 / GP(184)
C
C *** EXCLUSIVE ROUTINE ARRAYS
      DIMENSION GF(4),TEST(11)
C
      DATA GF /
     1 .173927423 , .326072577 , .326072577 , .173927423 /
      DATA DIV /    .861136312 /
C
      ITS=1
      VSE=0.0
      SSE=0.0
      RDP=0.0
      VDP=0.0
      RSE=0.0
      TSE=0.0
      ST1=2.0*V(LAY)
      ST2=1.0-ST1
      ST3=2.0*ST2
      ST4=1.0+ST1
      KSW5=1
      KSW6=1
      NAB=0
      NBZC=0
      K1=0
      K2=-3
      DO 36 K3=1,NBZ
      S1=0.0
      S2=0.0
      S3=0.0
      S4=0.0
      S5=0.0
      S6=0.0
      NBZC=NBZC+1
      DO 22 K4=1,NGQP
      K1=K1+1
      K2=K2+4
      FM=GP(K1)/WRL
      GO TO ( 9,8),KSW4
    8 VAL=1.0
      GO TO 11
    9 VAL=FM*WZ
      IF(VAL-TST1 ) 10,10,6
    6 KSW6=2
      GO TO 22
   10 VAL=EXP(VAL)
   11 T2=FM*AJ1(K1)*GF(K4)
      GO TO ( 13,12),KSW3
   12 T1=FM*T2
      GO TO 14
   13 T1=T2*RJ0(K1)
      T2=T2*RJ1(K1)
      T3=FM*T1
C *** C - D  SECTION
   14 D=CDAB(LAY,K2+1)
      CD=FM*(CDAB(LAY,K2)+D*WZ)/VAL
      DE=D/VAL
      GO TO ( 16,15),KSW3
   15 S1=S1+T1*(ST2*DE+CD)
      S4=S4+T2*(-ST3*DE-CD)
      S5=S5+T1*((ST1+0.5)*DE-0.5*CD)
      GO TO 17
   16 S1=S1+T3*(ST2*DE+CD)
      S2=S2+FM*T2*(-ST1*DE+CD)
      S3=S3+T2*(DE-CD)
      S5=S5+T3*(ST4*DE-CD)
      S4=S4-T1*(ST3*DE+CD)
      S6=S6+T3*DE
C *** A - B  SECTION
   17 GO TO ( 32,22),KSW5
   32 B=CDAB(LAY,K2+3)
      IF(B) 18,31,18
   31 KSW5=2
      NAB=NBZC
      GO TO 22
   18 AB=FM*(CDAB(LAY,K2+2)+B*WZ)*VAL
      BE=B*VAL
      GO TO ( 20,19),KSW3
   19 S1=S1+T1*(ST2*BE-AB)
      S4=S4+T2*(ST3*BE-AB)
      S5=S5+T1*((ST1+0.5)*BE+0.5*AB)
      GO TO 22
   20 S1=S1+T3*(ST2*BE-AB)
      S2=S2+FM*T2*(ST1*BE+AB)
      S3=S3+T2*(BE+AB)
      S4=S4+T1*(ST3*BE-AB)
      S5=S5+T3*(ST4*BE+AB)
      S6=S6+T3*BE
   22 CONTINUE
      SF=(GP(K1)-GP(K1-3))/(DIV*WRL)
      VSE=VSE+S1*SF
      VDP=VDP+S4*SF
      GO TO ( 24,23),KSW3
   23 RSE=RSE+S5*SF
C *** FORMULAS CHANGED FROM
C     TSE=TSE+ST1*S6*SF
C *** TO
      TSE=RSE
      GO TO 25
   24 SSE=SSE+S2*SF
      RDP=RDP+S3*SF
      RSE=RSE+SF*(S5-S3/WR)
      TSE=TSE+SF*(ST1*S6+S3/WR)
   25 TESTH=ABS(2.0*S1*RL*SF)-TST2
      IF(ITS-NTSI) 26,27,27
   26 TEST(ITS)=TESTH
      ITS=ITS+1
      GO TO 34
   27 TEST(NTSI)=TESTH
      DO 30 K4=1,NTEST
      IF(TESTH-TEST(K4)) 28,29,29
   28 TESTH=TEST(K4)
   29 TEST(K4)=TEST(K4+1)
   30 CONTINUE
      IF(TESTH) 42,42,34
   34 GO TO ( 36,43),KSW6
   36 CONTINUE
   41 IND=1
      GO TO 50
   42 IND=2
      GO TO 50
   43 IND=3
   50 RETURN
      END
      SUBROUTINE ELSY9
C *** FOR USE WITH ELSYM5
C *** ELSY9
C
      COMMON E(5),V(5),DI(5),R(100),Z(10),EX(32),
     1        CDAB(5,736),AJ1(184),RJ1(184),RJ0(184),
     2        VSE,SSE,RDP,VDP,RSE,TSE,ST1,ST2,ST3,ST4,
     3        WR,WZ,WRL,RL,PRES,RLP,TST1,TST2,TST3,TST4,
     4        NSYM,NSY,NLSW,NBZ,NGQP,NX,NEX,NR,NRC,NZ,NZC,MSW,
     5        NEL,NEI,NI,NBLL,LAY,NTEST,NTSI,ITS,IND,NAB,NBZC,
     6        KSW1,KSW2,KSW3,KSW4,KSW5,KSW6,KSW7,KSW8,KSW9
C
      COMMON XL(10),YL(10),XP(10),YP(10),LAYZ(10),
     1  ANS(6,100,10),AS(9,3,10),TITLE(14),IXR(100),NLD,NXY
C
C *** EXCLUSIVE ROUTINE ARRAYS
      DIMENSION A(6)
C
      A(1)=RSE*RLP
      A(2)=TSE*RLP
      A(3)=VSE*RLP
      A(4)=SSE*RLP
      A(5)=(1.0+V(LAY))/E(LAY)*RDP*RLP
      A(6)=(1.0+V(LAY))/E(LAY)*VDP*RLP
      ANS(1,NRC,NZC)=A(1)
      ANS(2,NRC,NZC)=A(2)
      ANS(3,NRC,NZC)=A(3)
      ANS(4,NRC,NZC)=A(4)
      ANS(5,NRC,NZC)=A(5)
      ANS(6,NRC,NZC)=A(6)
      RETURN
      END
      FUNCTION CHEB (A,N,Z)
C *** FOR USE WITH ELSYM5
C
      DIMENSION A(12),B(14)
      B(1)=0.
      B(2)=0.
      DO 1 I=1,N
      B(I+2)=Z*B(I+1)-B(I)+A(I)
    1 CONTINUE
      CHEB =.5*(B(N+2)-B(N))
      RETURN
      END
      SUBROUTINE ELSY10 (H,U,N,IVEC)
C *** FOR USE WITH ELSYM5
C     SYMMETRIC MATRIX
      DIMENSION H(3,3),U(3,3),W(3),IQ(3)
C
      AN=N
      NMI1=N-1
      IF(IVEC-1)6,1,6
    1 DO 5 I=1,N
      DO 4 J=1,N
      IF(I-J)3,2,3
    2 U(I,J)=1.0
      GO TO 4
    3 U(I,J)=0.0
    4 CONTINUE
    5 CONTINUE
    6 DO 9 I=1,NMI1
      W(I)=0.0
      IPL1=I+1
      DO 8 J=IPL1,N
      IF(W(I)-ABS(H(I,J)))7,7,8
    7 W(I)=ABS(H(I,J))
      IQ(I)=J
    8 CONTINUE
    9 CONTINUE
   10 DO 12 I=1,NMI1
C
      IF(I-1) 11,11,17
   17 IF(XMAX-W(I)) 11,12,12
C
   11 XMAX=W(I)
      IPIV=I
      JPIV=IQ(I)
   12 CONTINUE
      IF(XMAX-1.E-12/AN)30,30,13
   13 Z=H(IPIV,IPIV)-H(JPIV,JPIV)
C
C     Y=2.D0*H(IPIV,JPIV) ***
C     TA=Y/(DABS(Z)+DSQRT(Z*Z+Y*Y)) ***
C     IF(Z.LT.0.0) TA=-TA ***
C     CO=1.D0/DSQRT(1.D0+TA*TA) ***
      Y=2.0*H(IPIV,JPIV)
      TA=Y/(ABS(Z)+SQRT(Z*Z+Y*Y))
      IF(Z) 18,19,19
   18 TA=-TA
   19 CO=1.0/SQRT(1.0+TA*TA)
C
      SI=TA*CO
      HII=H(IPIV,IPIV)
      HJJ=H(JPIV,JPIV)
      HIJ=H(IPIV,JPIV)
      DO 14 K=1,N
      HTE=H(K,IPIV)
      H(K,IPIV)=H(K,IPIV)*CO+H(K,JPIV)*SI
      H(K,JPIV)=H(K,JPIV)*CO-HTE*SI
      H(IPIV,K)=H(K,IPIV)
   14 H(JPIV,K)=H(K,JPIV)
      H(IPIV,JPIV)=0
      H(JPIV,IPIV)=0
      AA=HIJ*TA
      H(IPIV,IPIV)=HII+AA
      H(JPIV,JPIV)=HJJ-AA
      IF(IVEC) 6,6,15
   15 DO 16 K=1,N
      UTE=U(K,IPIV)
      U(K,IPIV)=U(K,IPIV)*CO+U(K,JPIV)*SI
   16 U(K,JPIV)=U(K,JPIV)*CO-UTE*SI
      GO TO 6
   30 RETURN
      END
      SUBROUTINE ELSY11
C *** FOR USE WITH ELSYM5
C
      COMMON E(5),V(5),DI(5),R(100),Z(10),EX(32),
     1        CDAB(5,736),AJ1(184),RJ1(184),RJ0(184),
     2        VSE,SSE,RDP,VDP,RSE,TSE,ST1,ST2,ST3,ST4,
     3        WR,WZ,WRL,RL,PRES,RLP,TST1,TST2,TST3,TST4,
     4        NSYM,NSY,NLSW,NBZ,NGQP,NX,NEX,NR,NRC,NZ,NZC,MSW,
     5        NEL,NEI,NI,NBLL,LAY,NTEST,NTSI,ITS,IND,NAB,NBZC,
     6        KSW1,KSW2,KSW3,KSW4,KSW5,KSW6,KSW7,KSW8,KSW9
C
      COMMON XL(10),YL(10),XP(10),YP(10),LAYZ(10),
     1  ANS(6,100,10),AS(9,3,10),TITLE(14),IXR(100),NLD,NXY
C
C *** EXCLUSIVE ROUTINE ARRAYS
      DIMENSION A(6),SET(3,3),DC(3,3),T(3)
      CHARACTER LAB1(6,9),LAB2(3,9)
C
      DATA LAB1 /
     1 'NORM','AL S','TRES','SES',' ',' ',
     2 'SHEA','R ST','RESS','ES',' ',' ',
     3 'PRIN','CIPA','L ST','RESS','ES',' ',
     4 'PRIN','CIPA','L SH','EAR ','STRE','SSES',
     5 'DISP','LACE','MENT','S',' ',' ',
     6 'NORM','AL S','TRAI','NS',' ',' ',
     7 'SHEA','R ST','RAIN','S',' ',' ',
     8 'PRIN','CIPA','L ST','RAIN','S',' ',
     9 'PRIN','CIPA','L SH','EAR ','STRA','INS'/
C
      DATA LAB2 / 3HSXX,3HSYY,3HSZZ,3HSXY,3HSXZ,3HSYZ,
     1 4HPS 1,4HPS 2,4HPS 3,4HPSS1,4HPSS2,4HPSS3,2HUX,2HUY,
     2 2HUZ,3HEXX,3HEYY,3HEZZ,3HEXY,3HEXZ,3HEYZ,4HPE 1,4HPE 2,
     3 4HPE 3,4HPSE1,4HPSE2,4HPSE3/
C
      DO 8 NZC=1,NZ
      LAY=LAYZ(NZC)
      ELAY=E(LAY)
      VLAY=V(LAY)
      SSC=2.0*(1.0+VLAY)/ELAY
      DO 9 K1=1,9
      DO 9 K2=1,3
      DO 9 K3=1,NXY
      AS(K1,K2,K3)=0.0
    9 CONTINUE
      K9=1
      DO 10 NXYC=1,NXY
      XPI=XP(NXYC)
      YPI=YP(NXYC)
      DO 14 NLDC=1,NLD
      IR=IXR(K9)
      DO 16 K1=1,6
      A(K1)=ANS(K1,IR,NZC)
   16 CONTINUE
      RI=R(IR)
      IF(RI-.001) 13,13,11
   11 SINT=(YPI-YL(NLDC))/RI
      SINT2=SINT**2
      COST=(XPI-XL(NLDC))/RI
      COST2=COST**2
C *** NORMAT STRESSES
C *** SXX
      AS(1,1,NXYC)=AS(1,1,NXYC)+COST2*A(1)+SINT2*A(2)
C *** SYY
      AS(1,2,NXYC)=AS(1,2,NXYC)+COST2*A(2)+SINT2*A(1)
C *** SZZ
      AS(1,3,NXYC)=AS(1,3,NXYC)+A(3)
C *** SHEAR STRESSES
C *** SXY
      AS(2,1,NXYC)=AS(2,1,NXYC)+COST*SINT*(A(1)-A(2))
C *** SXZ
      AS(2,2,NXYC)=AS(2,2,NXYC)+COST*A(4)
C *** SYZ
      AS(2,3,NXYC)=AS(2,3,NXYC)+SINT*A(4)
C *** DISPLACEMENTS
C *** UX
      AS(5,1,NXYC)=AS(5,1,NXYC)+COST*A(5)
C *** UY
      AS(5,2,NXYC)=AS(5,2,NXYC)+SINT*A(5)
C *** UZ
      AS(5,3,NXYC)=AS(5,3,NXYC)+A(6)
      GO TO 15
C
   13 AS(1,1,NXYC)=AS(1,1,NXYC)+A(1)
      AS(1,2,NXYC)=AS(1,2,NXYC)+A(2)
      AS(1,3,NXYC)=AS(1,3,NXYC)+A(3)
      AS(2,2,NXYC)=AS(2,2,NXYC)+A(4)
      AS(5,1,NXYC)=AS(5,1,NXYC)+A(5)
      AS(5,3,NXYC)=AS(5,3,NXYC)+A(6)
   15 K9=K9+1
   14 CONTINUE
      DO 20 K1=1,3
      SET(K1,K1)=AS(1,K1,NXYC)
   20 CONTINUE
      SET(1,2)=AS(2,1,NXYC)
      SET(1,3)=AS(2,2,NXYC)
      SET(2,3)=AS(2,3,NXYC)
      SET(2,1)=SET(1,2)
      SET(3,1)=SET(1,3)
      SET(3,2)=SET(2,3)
      CALL ELSY10(SET,DC,3,0)
      DO 22 K1=1,3
      T(K1)=SET(K1,K1)
   22 CONTINUE
      DO 24 K1=1,2
      K2=K1+1
      DO 24 K3=K2,3
      IF(T(K1)-T(K3)) 26,24,24
   26 TEMP=T(K1)
      T(K1)=T(K3)
      T(K3)=TEMP
   24 CONTINUE
C *** PRINCIPAL STRESSES
C *** PS 1
      AS(3,1,NXYC)=T(1)
C *** PS 2
      AS(3,2,NXYC)=T(2)
C *** PS 3
      AS(3,3,NXYC)=T(3)
C *** PRINCIPAL SHEAR STRESSES
C *** PSS 1
      AS(4,1,NXYC)=0.5*(T(1)-T(3))
C *** PSS 2
      AS(4,2,NXYC)=0.5*(T(1)-T(2))
C *** PSS 3
      AS(4,3,NXYC)=0.5*(T(2)-T(3))
C *** NORMAT STRAINS
C *** EXX
      AS(6,1,NXYC)=(AS(1,1,NXYC)-VLAY*(AS(1,2,NXYC)+AS(1,3,NXYC)))/ELAY
C *** EYY
      AS(6,2,NXYC)=(AS(1,2,NXYC)-VLAY*(AS(1,1,NXYC)+AS(1,3,NXYC)))/ELAY
C *** EZZ
      AS(6,3,NXYC)=(AS(1,3,NXYC)-VLAY*(AS(1,1,NXYC)+AS(1,2,NXYC)))/ELAY
C *** SHEAR STRAINS
C *** EXY
      AS(7,1,NXYC)=SSC*AS(2,1,NXYC)
C *** EXZ
      AS(7,2,NXYC)=SSC*AS(2,2,NXYC)
C *** EYZ
      AS(7,3,NXYC)=SSC*AS(2,3,NXYC)
C *** PRINCIPAL STRAINS
C *** PE 1
      AS(8,1,NXYC)=(T(1)-VLAY*(T(2)+T(3)))/ELAY
C *** PE 2
      AS(8,2,NXYC)=(T(2)-VLAY*(T(1)+T(3)))/ELAY
C *** PE 3
      AS(8,3,NXYC)=(T(3)-VLAY*(T(1)+T(2)))/ELAY
C *** PRINCIPAL SHEAR STRAINS
C *** PSE 1
      AS(9,1,NXYC)=SSC*AS(4,1,NXYC)
C *** PSE 2
      AS(9,2,NXYC)=SSC*AS(4,2,NXYC)
C *** PSE 3
      AS(9,3,NXYC)=SSC*AS(4,3,NXYC)
   10 CONTINUE
      WRITE(6,1020)
 1020 FORMAT (1H1,4X,'ELSYM5  5/80 - 3A  ELASTIC LAYERED SYSTEM WITH ONE
     1 TO TEN NORMAL IDENTICAL CIRCULAR UNIFORM LOAD(S)')
      WRITE(6,1030) NSY,TITLE
 1030 FORMAT (1H0,4X,'ELASTIC SYSTEM'I3,' - ',14A4)
      WRITE(6,1031) Z(NZC),LAY
 1031 FORMAT (1H0,6X,2HZ=,F7.2,10H  LAYER NO,I3)
      WRITE(6,1032) (XP(K1),K1=1,NXY)
 1032 FORMAT (1H0,6X,2HX=,10F11.2)
      WRITE(6,1033) (YP(K1),K1=1,NXY)
 1033 FORMAT (1H ,6X,2HY=,10F11.2)
      DO 30 K1=1,9
      WRITE(6,1034) (LAB1(K2,K1),K2=1,6)
 1034 FORMAT (1H0,4X,6A4)
      DO 30 K3=1,3
      WRITE(6,1035) LAB2(K3,K1),(AS(K1,K3,K2),K2=1,NXY)
 1035 FORMAT (1H ,5X,A4,10E11.4)
   30 CONTINUE
    8 CONTINUE
      RETURN
      END
      FUNCTION ELSY12 (X,N)
C *** FOR USE WITH ELSYM5
C
C *** EXCLUSIVE ROUTINE ARRAYS
      REAL B1(12),B2(5),B3(5),B4(12),B5(5),B6(5)
      REAL B7(12),B8(5),B9(5)
C
      DATA B1    /-.000000003 , .000000076 ,
     1-.000001762 , .000032460 ,-.000460626 , .004819180 ,-.034893769 ,
     2 .158067102 ,-.370094994 , .265178613 ,-.008723442 , .315455943 /
      DATA B2 /
     1-.000000001 , .000000018 ,-.000000741 , .000068385 ,-.031111709 /
      DATA B3 /
     1 .000000002 ,-.000000052 , .000003075 ,-.000536522 , 1.99892070 /
C
      DATA B4    /-.000000001 , .000000029 ,
     1-.000000762 , .000015887 ,-.000260444 , .003240270 ,-.029175525 ,
     2 .177709120 ,-.661443934 , 1.28799410 ,-1.19180120 , 1.29671754 /
      DATA B5 /
     1 .000000001 ,-.000000021 , .000000914 ,-.000096277 , .093555574 /
      DATA B6 /
     1-.000000002 , .000000062 ,-.000003987 , .000898990 , 2.00180610 /
C
      DATA B7    / 0.0        , .000000010 ,
     1-.000000300 , .000006870 ,-.000127390 , .001829980 ,-.019569010 ,
     2 .147707600 ,-.730405480 , 2.13203940 ,-3.30639360 , 3.32384070 /
      DATA B8 /
     1 0.0        , .000000040 ,-.000001730 , .000293150 , .469339820 /
      DATA B9 /0.0,-0.000000112,0.000008970,-0.006371780,1.98723830/
C
      NP1=N+1
      IF(X-8.) 14,14,22
   14 XZ=X*X*.0625-2.
      GO TO (16,18,20),NP1
   16 BESS=CHEB(B1,12,XZ)
      GO TO 48
   18 BESS=X*CHEB(B4,12,XZ)/8.0
      GO TO 48
   20 BESS=X*X/64.0*CHEB(B7,12,XZ)
      GO TO 48
   22 XZ=256./(X*X)-2.0
      A=0.78539816
      B=2.3561945
      C=0.797884561
      D=3.9269908
      GO TO (24,26,28),NP1
   24 BESS=-(8.0/X)*CHEB(B2,5,XZ)*SIN(X-A)
      BESS=BESS+CHEB(B3,5,XZ)*COS(X-A)
      BESS=BESS*C/SQRT(X)
      GO TO 48
   26 BESS=-(8.0/X)*CHEB(B5,5,XZ)*SIN(X-B)
      BESS=BESS+CHEB(B6,5,XZ)*COS(X-B)
      BESS=BESS*C/SQRT(X)
      GO TO 48
   28 BESS=-(8.0/X)*CHEB(B8,5,XZ)*SIN(X-D)
      BESS=BESS+CHEB(B9,5,XZ)*COS(X-D)
      BESS=BESS*C/SQRT(X)
   48 ELSY12=BESS
      RETURN
      END

